/**
 * The first line in the build configuration applies the Android Gradle plugin
 * to this build and makes the android block available to specify
 * Android-specific build options.
 */

plugins {
    id 'com.android.application'
    id 'com.diffplug.spotless'
    id 'com.google.dagger.hilt.android'
    id 'pmd'
}

android {
    /**
     * The app's namespace. Used primarily to access app resources.
     */

    namespace 'com.kresshy.weatherstation'
    compileSdk 35

    lint {
        abortOnError false
        checkDependencies true
        ignoreWarnings false
        warningsAsErrors true
        checkReleaseBuilds true
        explainIssues true
        xmlReport true
        htmlReport true
    }

    /**
     * compileSdk specifies the Android API level Gradle should use to
     * compile your app. This means your app can use the API features included in
     * this API level and lower.
     */


    /**
     * The defaultConfig block encapsulates default settings and entries for all
     * build variants and can override some attributes in main/AndroidManifest.xml
     * dynamically from the build system. You can configure product flavors to override
     * these values for different versions of your app.
     */

    defaultConfig {
        // Uniquely identifies the package for publishing.
        applicationId "com.kresshy.weatherstation"

        // Defines the minimum API level required to run the app.
        minSdk 23

        // Specifies the API level used to test the app.
        targetSdk 35

        // Defines the version number of your app.
        versionCode 51

        // Defines a user-friendly version name for your app.
        versionName "3.0.6"
    }

    /**
     * The buildTypes block is where you can configure multiple build types.
     * By default, the build system defines two build types: debug and release. The
     * debug build type is not explicitly shown in the default build configuration,
     * but it includes debugging tools and is signed with the debug key. The release
     * build type applies ProGuard settings and is not signed by default.
     */

    buildTypes {

        /**
         * By default, Android Studio configures the release build type to enable code
         * shrinking, using minifyEnabled, and specifies the default ProGuard rules file.
         */

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

spotless {
    java {
        target 'src/*/java/**/*.java'
        // apply a specific flavor of google-java-format
        googleJavaFormat('1.24.0')
                .aosp()
                .reflowLongStrings()
                .reorderImports(true)
                .formatJavadoc(true)
        // fix formatting of type annotations
        formatAnnotations()
    }
    format 'misc', {
        target '**/*.gradle', '**/*.md', '**/.gitignore'
        leadingTabsToSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}


/**
 * The dependencies block in the module-level build configuration file
 * specifies dependencies required to build only the module itself.
 * To learn more, go to Add build dependencies.
 */


configurations.all {
    resolutionStrategy {
        force 'org.jetbrains.kotlin:kotlin-stdlib:1.8.22'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.10.0'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.10.0'
    implementation 'androidx.lifecycle:lifecycle-service:2.10.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.navigation:navigation-fragment:2.8.7'
    implementation 'androidx.navigation:navigation-ui:2.8.7'
    implementation 'com.google.android.material:material:1.12.0'
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation 'com.google.dagger:hilt-android:2.51.1'
    annotationProcessor 'com.google.dagger:hilt-compiler:2.51.1'

    implementation 'com.google.auto.value:auto-value-annotations:1.11.0'
    annotationProcessor 'com.google.auto.value:auto-value:1.11.0'

    implementation 'com.google.code.gson:gson:2.13.2'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'org.robolectric:robolectric:4.14.1'
    testImplementation 'androidx.test:core:1.6.1'
    testImplementation 'androidx.test.ext:junit:1.2.1'
    testImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    testImplementation 'androidx.test.espresso:espresso-contrib:3.6.1'
    testImplementation 'com.google.dagger:hilt-android-testing:2.51.1'
    testAnnotationProcessor 'com.google.dagger:hilt-android-compiler:2.51.1'
}

pmd {
    toolVersion = "7.0.0"
    ignoreFailures = true
}

task pmd(type: Pmd) {
    description 'Run PMD to find dead code and unused variables'
    group 'verification'
    source = fileTree('src/main/java')
    ruleSets = ["category/java/errorprone.xml", "category/java/bestpractices.xml"]
    reports {
        xml.required = true
        html.required = true
    }
}
